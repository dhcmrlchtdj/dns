pkgname=godns
pkgver=0.0.1
pkgrel=2
pkgdesc='DNS server'
arch=('any')
url="https://github.com/dhcmrlchtdj/godns"
license=('AGPL-3.0-or-later')
makedepends=('go' 'git')
backup=(etc/godns/config.json)
source=(
    "config.json"
    "godns.service"
    "$pkgname::git+https://github.com/dhcmrlchtdj/godns.git"
)
sha256sums=(
    '3faa9d81024a1f283a07a7b0cc3142b82a85cb64b1264ce7751eca3430d3eeaf'
    'b9691eb247cbe422cccbc04d6304564ec5038affcdb2f598e357d363f05b72a7'
    'SKIP'
)

prepare(){
    cd "${pkgname}"
    mkdir -p build/
}

build() {
    cd "${pkgname}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
    go build -o build .
}

package() {
    cd "${pkgname}"
    install -Dm755 "build/godns" "${pkgdir}/usr/bin/${pkgname}"
    install -Dm644 "${srcdir}/godns.service" "${pkgdir}/usr/lib/systemd/system/godns.service"
    install -Dm644 "${srcdir}/config.json" "${pkgdir}/etc/godns/config.json"
}
