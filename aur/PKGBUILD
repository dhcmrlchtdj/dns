pkgname=godns-git
pkgver=r49.867a3fb
pkgrel=1
pkgdesc='DNS server'
arch=('any')
url="https://github.com/dhcmrlchtdj/godns"
license=('Apache-2.0')
makedepends=('go' 'git')
provides=('godns')
backup=(etc/godns/config.json)
source=(
    "config.json"
    "godns.service"
    "${pkgname}::git+https://github.com/dhcmrlchtdj/godns.git"
)
sha256sums=(
    'fb854cdb4ff952aee393e8e77ea0555737c8ba1907a95ed7b74672e340a38580'
    'b9691eb247cbe422cccbc04d6304564ec5038affcdb2f598e357d363f05b72a7'
    'SKIP'
)

pkgver() {
    cd "${pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
    cd "${pkgname}"
    mkdir -p build/
}

build() {
    cd "${pkgname}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-trimpath -buildmode=pie -buildvcs=false -ldflags=-linkmode=external -modcacherw"
    go build -o build .
}

package() {
    cd "${pkgname}"
    install -Dm755 "build/godns" "${pkgdir}/usr/bin/godns"
    install -Dm644 "${srcdir}/godns.service" "${pkgdir}/usr/lib/systemd/system/godns.service"
    install -Dm644 "${srcdir}/config.json" "${pkgdir}/etc/godns/config.json"
}
