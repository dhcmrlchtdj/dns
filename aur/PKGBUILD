pkgname=godns-git
pkgver=r49.867a3fb
pkgrel=1
pkgdesc='DNS server'
arch=('any')
url="https://github.com/dhcmrlchtdj/godns"
license=('Apache-2.0')
makedepends=('go' 'git')
provides=('godns')
backup=(etc/godns/config.json)
source=(
	"config.json"
	"godns.service"
	"${pkgname}::git+https://github.com/dhcmrlchtdj/godns.git"
)
sha256sums=(
	'f2e15d1dd85f3f218835d3bd3921c4e7bebb5d9bc07f6b1ad5a2ed46aa9caaa9'
	'470195261a5933bb4f072e9a05c8676b31b45c9e64fc4f6290e443fef84ee7ad'
	'SKIP'
)

pkgver() {
	cd "${pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
	cd "${pkgname}"
	mkdir -p build/
}

build() {
	cd "${pkgname}"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-trimpath -buildmode=pie -buildvcs=false -mod=readonly -modcacherw"
	export GOEXPERIMENT=loopvar
	go build -o build/ ./cmd/...
}

package() {
	cd "${pkgname}"
	install -Dm755 "build/godns" "${pkgdir}/usr/bin/godns"
	install -Dm644 "${srcdir}/godns.service" "${pkgdir}/usr/lib/systemd/system/godns.service"
	install -Dm644 "${srcdir}/config.json" "${pkgdir}/etc/godns/config.json"
}
